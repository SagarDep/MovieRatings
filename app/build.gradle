apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Playstore")) {
    apply plugin: 'io.fabric'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

ext.versionMajor = 0
ext.versionMinor = 2
ext.versionPatch = 0

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        applicationId "com.fenchtose.flutter"
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'OMDB_API_KEY', properties['omdb_api_key']
    }
    productFlavors {
        playstore {
            applicationId "com.fenchtose.flutter"
        }

        foss {
            applicationIdSuffix ".foss"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    playstoreCompile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile ('androidx.core:core-ktx:0.2', {
        exclude group: 'com.android.support'
    })
    // Android TV
    //    compile 'com.android.support:leanback-v17:25.3.1'
    compile 'io.reactivex.rxjava2:rxkotlin:2.0.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    // Room
    compile 'android.arch.persistence.room:runtime:1.0.0'
//    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'
    kapt 'android.arch.persistence.room:compiler:1.0.0'
    compile 'com.github.bumptech.glide:glide:4.0.0-RC0'
    testCompile 'junit:junit:4.12'
    // For sqlite db debug
    debugCompile 'com.amitshekhar.android:debug-db:1.0.1'

    // stetho
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // Leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.fabric.io/public'
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}
